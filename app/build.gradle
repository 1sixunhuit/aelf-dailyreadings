apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "co.epitre.aelf_lectures"
        minSdkVersion 16
        targetSdkVersion 29
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha01'

    // Material design
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha02'

    // HTTP layer
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // Foundation
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Sqlite
    implementation project(':sqlite-android-3290000')
}

task preLoadReadings(type: Exec) {
    commandLine '../scripts/preload-readings.sh'
}
preBuild.dependsOn preLoadReadings

android {
    lintOptions {
        checkReleaseBuilds false // Add this
        abortOnError false
    }
}

//
// Custom tasks
//

class DownloadFile extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
