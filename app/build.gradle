apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "co.epitre.aelf_lectures"
        minSdkVersion 16
        targetSdkVersion 29
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
        }
    }
    lintOptions {
        checkReleaseBuilds false // Add this
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0-SNAPSHOT' // WARN: 1.2.0-alpha01 cause webview inflate crash on Android 5.0

    // Material design
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha03'

    // HTTP layer
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // Foundation
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Sqlite
    implementation project(':sqlite-android-3290000')
}

task downloadMarkJs(type: DownloadFile) {
    sourceUrl = 'https://raw.githubusercontent.com/julmot/mark.js/8.11.1/dist/mark.min.js'
    target = new File('./src/main/assets/js/mark.8.11.1.min.js')
}
preBuild.dependsOn downloadMarkJs

//
// FIXME: remove after androidx.appcompat:appcompat >= 1.2.0-alpha02 is release
//

repositories {
    maven {
        url 'https://ci.android.com/builds/submitted/6052626/androidx_snapshot/latest/repository/'
    }
}

//
// Custom tasks
//

class DownloadFile extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
